plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')

    forgeRuntimeLibrary implementation("org.eclipse.jgit:org.eclipse.jgit:6.10.0.202406032230-r")
    shadowBundle("org.eclipse.jgit:org.eclipse.jgit:6.10.0.202406032230-r")

    forgeRuntimeLibrary implementation("org.apache.sshd:sshd-core:2.14.0")
    shadowBundle("org.apache.sshd:sshd-core:2.14.0")

    forgeRuntimeLibrary implementation("org.apache.sshd:sshd-common:2.14.0")
    shadowBundle("org.apache.sshd:sshd-common:2.14.0")
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
